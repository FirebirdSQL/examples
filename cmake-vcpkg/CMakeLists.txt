cmake_minimum_required(VERSION 3.18)

set(VCPKG_ROOT "../vcpkg")
set(CMAKE_TOOLCHAIN_FILE "${VCPKG_ROOT}/scripts/buildsystems/vcpkg.cmake" CACHE STRING "")

set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_POSITION_INDEPENDENT_CODE ON)
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_CXX_VISIBILITY_PRESET hidden)
set(CMAKE_INSTALL_RPATH "\$ORIGIN/../lib")
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

get_property(IS_MULTI_CONFIG GLOBAL PROPERTY GENERATOR_IS_MULTI_CONFIG)

if(WIN32)
	set(DYNLIB_DIRECTORY bin)
else()
	set(DYNLIB_DIRECTORY lib)
endif()

if(IS_MULTI_CONFIG)
	set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/$<CONFIG>/bin)
	set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/$<CONFIG>/lib)
	set(INSTALL_SUFFIX $<CONFIG>)
else()
	set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)
	set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
	set(INSTALL_SUFFIX .)
endif()

# Avoid copy of only fbclient.dll in Windows.
option(VCPKG_APPLOCAL_DEPS OFF)


project(firebird-vcpkg-example)

enable_language(CXX)

if(MSVC)
	add_definitions(-D_CRT_SECURE_NO_WARNINGS)
endif()

if(MSVC)
	add_compile_options(/W3)
else()
	add_compile_options(-Wall)
endif()

find_package(firebird CONFIG REQUIRED)

add_executable(${PROJECT_NAME}
	src/main.cpp
)

set_property(TARGET ${PROJECT_NAME} PROPERTY
	VS_DEBUGGER_ENVIRONMENT "PATH=${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}$<$<CONFIG:Debug>:/debug>/bin"
)

target_link_libraries(${PROJECT_NAME}
	PRIVATE firebird
)


install(
	TARGETS ${PROJECT_NAME}
	RUNTIME
	DESTINATION ${INSTALL_SUFFIX}/bin
)

install(
	DIRECTORY
		"${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/${DYNLIB_DIRECTORY}"
		"${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/plugins"
		"${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/share"
	USE_SOURCE_PERMISSIONS
	CONFIGURATIONS Release
	DESTINATION ${CMAKE_INSTALL_PREFIX}/${INSTALL_SUFFIX}
	PATTERN "*.cmake" EXCLUDE
	PATTERN "vcpkg*" EXCLUDE
)

install(
	DIRECTORY
		"${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/debug/${DYNLIB_DIRECTORY}"
		"${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/debug/plugins"
		"${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/debug/share"
	USE_SOURCE_PERMISSIONS
	CONFIGURATIONS Debug
	DESTINATION ${CMAKE_INSTALL_PREFIX}/${INSTALL_SUFFIX}
	PATTERN "*.cmake" EXCLUDE
	PATTERN "vcpkg*" EXCLUDE
)
